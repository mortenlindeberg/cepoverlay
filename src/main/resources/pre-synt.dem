set terminal eps font "Times-New-Roman,14"
set output 'pre-synt.eps'

set style line 1 lt 1 lw 4 lc rgb '#0072bd' # blue
set style line 2 lt 2 lw 4 lc rgb '#d95319' # orange
set style line 3 lt 3 lw 4 lc rgb '#edb120' # yellow
set style line 4 lt 4 lw 4 lc rgb '#7e2f8e' # purple
set style line 5 lt 5 lw 4 lc rgb '#77ac30' # green
set style line 6 lt 6 lw 4 lc rgb '#4dbeee' # light-blue
set style line 7 lt 7 lw 4 lc rgb '#a2142f' # red

set xlabel 'Future window F_w'
set xrange [30:-2] reverse
set xtics ("React." 0, "5" 5, "10" 10, "15" 15, "20" 20, "25" 25, "30" 30)
set arrow from 2, graph 0 to 2, graph 1 lw 1.5 lc "black" dt 2 nohead
set key left

set multiplot layout 1, 2

set ylabel 'Bandwidth Consumption (Kb)'
set yrange [450:920]
plot "<echo '0 625.2'" ls 1 with point title 'Static I', \
     "<echo '0 582.2'" ls 2 with point title 'Static E_1', \
     "<echo '0 743.4'" ls 3 with point title 'Static E_2', \
     "<echo '0 482'" ls 4 with point title 'Reactive', \
     'pre-synt.txt' using 1:3 with linespoints ls 5 title 'Proactive', \
     'pre-synt.txt' using 1:5 with linespoints ls 6 title 'RA'



set ylabel 'E2E Latency (ms)'
set yrange [14:24]

plot "<echo '0 15.460576'" ls 1 with point title 'Static I', \
     "<echo '0 14.884694'" ls 2 with point title 'Static E_1', \
     "<echo '0 19.825752'" ls 3 with point title 'Static E_2', \
     "<echo '0 17.224923'" ls 4 with point title 'Reactive', \
     'pre-synt.txt' using 1:($2*1000) with linespoints ls 5 title 'Proactive', \
     'pre-synt.txt' using 1:($4*1000) with linespoints ls 6 title 'RA'

unset multiplot

